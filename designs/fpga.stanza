#use-added-syntax(esir)
defpackage demo :
  import core
  import collections
  import math
  import esir
  import esir/utils
  import esir/gen
  import tests/default-harness
  import repl-lib
  import checks

  import bundles
  import land-patterns
  import generator-utils
  import generic-components


pcb-component power-connector (v-out:Double) :
  port src:power
  spice: 
    "[V] {src.vdd} {src.gnd} {v-out}"

DESIGN-TEMPERATURE = 25.0

pcb-module demo :

  inst power : {power-connector(5.0)}

  properties(power.src.vdd):
    supply-pin => true
    voltage => [4.95 5.0 5.05]

  inst fmc : {samtec/ASP-134488-01/component}
  net gnd (fmc.gnd)

  inst ldo-15 : {texas-instruments/TLV743P/module(1.5)}
  inst ldo-25 : {texas-instruments/TLV743P/module(2.5)}
  inst ldo-33 : {texas-instruments/TLV743P/module(3.3)}
  inst fpga : {microsemi/A2F200M3F-FGG256I/module}

  net vin (power.src.vdd ldo-33.vin ldo-33.en)
  net P3V3 (ldo-33.vout, fpga.src-3V3.vdd)
  net (gnd power.src.gnd ldo-33.gnd fpga.src-3V3.gnd)

  net (vin ldo-25.vin ldo-25.en)
  net P2V5 (ldo-25.vout, fpga.src-io.vdd)
  net (gnd ldo-25.gnd, fpga.src-io.gnd)

  net (P2V5 ldo-15.vin ldo-15.en) 
  net P1V5 (ldo-15.vout, fpga.src-1V5.vdd)
  net (gnd ldo-15.gnd fpga.src-1V5.gnd)

  require out:gpio[20] from fmc
  require in:gpio[20] from fmc
  net (in out)

  require out-d:lvds[20] from fmc
  require in-d:lvds[20] from fpga
  net (in-d out-d)

  spice:
    ".tran 0.001ms 1m"
    ".inc PMBT3904.lib"
    ".inc TLV74315P_TRANS.LIB"
    ".inc TLV74325P_TRANS.LIB"
    ".inc TLV74333P_TRANS.LIB"
    ".options gmin=1e-10"
    ".options abstol=1e-7"
    ".options reltol=0.003"
    ".options cshunt=1e-15"

  check-design()

pcb-design main-design :
  module = demo

print-esir("input.esir")
export-xml("fpga.xml" [`lowered => true `ground-reference => demo.gnd `check-log => "checks-3.txt"])
