#use-added-syntax(esir)
defpackage design-generator :
  import core
  import collections
  import math
  import esir
  import esir/utils
  import esir/gen
  import esir/repl-lib
  import jitpcb/visualizer
  import ocdb/tests/default-harness
  import ocdb/generator-utils
  import ocdb/bundles
  import ocdb/design-vars
  import ocdb/generic-components
  import ocdb/power-regulators

OPERATING-TEMPERATURE = [-20.0 50.0]
OPTIMIZE-FOR = ["area"]
  
pcb-module sensor-mote :
  ; Power connector
  inst j : {pin-header(2)}

  ; Set up power solver
  set-power-source(j.p[1], j.p[2], 4.0)
  net gnd (j.p[2])

  ; Instantiate NRF52 Module
  inst proc : {ocdb/nordic/nRF52832/module}
  attach-programming-connector(proc, proc.pow3v0, "swd")

  ; Add and connect thermocouple sensor
  inst thermocouple : {ocdb/microchip/MCP9600/module}

  inst K : {pin-header(2)}
  net (K.p[1], thermocouple.in.P)
  net (K.p[2], thermocouple.in.N)

  require data:i2c from proc
  net THERMO (data, thermocouple.i2c)

  require alert:gpio[4] from proc
  for i in 0 to 4 do :
    net ALERT[i] (alert[i].gpio, thermocouple.alert[i])

  ; Add geometric constraints
  package(proc) at loc(0.0, 0.0) on Top
  package(j) at loc(-14.0, 0.0) on Top

  ; Apply groups to sub-circuits for schematic and layout
  group-modules([[proc] [thermocouple K]])
  symbol(gnd) = {ocdb/symbols/ground-sym}

val main-design = default-board(sensor-mote, 4, 32.0,12.0)

lower-esir()
run-user-pass(generate-power)

view(main-design)

export-cad("BLE-mote", "kicad")