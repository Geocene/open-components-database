#use-added-syntax(esir)
; Include space before colon in definitions
defpackage ocdb/cree/CLMUC-FK :
  import core
  import collections
  import math
  import esir
  import esir/utils
  import esir/gen
  import ocdb/tests/default-harness

  import ocdb/land-patterns
  import ocdb/symbols
  import ocdb/box-symbol
  import ocdb/pinspec
  import ocdb/bundles
  import ocdb/generator-utils
  import ocdb/generic-components

  import jitpcb/visualizer

pcb-package package-rgb :
; Assign repetitive code to variables
  val xoff = 0.57
  val yoff = 0.35
  val smd-pad = smd-pad(0.55, 0.45)

  pad p[1] : {smd-pad} at loc((- xoff), yoff)
  pad p[2] : {smd-pad} at loc((- xoff), (- yoff))
  pad p[3] : {smd-pad} at loc(xoff, (- yoff))
  pad p[4] : {smd-pad} at loc(xoff, yoff)

  layer(Courtyard(Top)) = Rectangle(1.5, 1.5)
  layer(Silkscreen("f-silk", Top)) = Rectangle(1.7, 1.7)
  ref-label()

; Group together pins that can be easily named where it makes sense
pcb-bundle leds :
  pin r
  pin g
  pin b

public unique pcb-component component :
  manufacturer = "Cree"
  mpn = "CLMUC-FKA-CL8LCFL5L8BB79353"
  description = "3 in 1 RGB SMD LED"

  pin a
; Use unified generator to create pins
  port leds : leds 
  symbol = diode-rgb-led(a => a, leds.r => r, leds.g => g, leds.b => b)
  package = package-rgb(a => p[1], leds.r => p[2], leds.g => p[3], 
                        leds.b => p[4])

public unique pcb-module module :
; Define pins or ports to externally interface with other modules
  port leds : leds
  pin vcc
  inst led : {ocdb/cree/CLMUC-FK/component}
  inst r-res : {gen-res-cmp(470.0)}
  inst g-res : {gen-res-cmp(330.0)}
  inst b-res : {gen-res-cmp(100.0)}

  net (led.leds.r, r-res.p[1])
  net (led.leds.g, g-res.p[1])
  net (led.leds.b, b-res.p[1])

  net (leds.r, r-res.p[2])
  net (leds.g, g-res.p[2])
  net (leds.b, b-res.p[2])
  net (vcc, led.a)
