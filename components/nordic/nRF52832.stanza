#use-added-syntax(esir)
defpackage ocdb/nordic/nRF52832 :
  import core
  import collections
  import math
  import esir
  import esir/utils
  import esir/gen
  import ocdb/tests/default-harness

  import ocdb/land-patterns
  import ocdb/symbols
  import ocdb/box-symbol
  import ocdb/pinspec
  import ocdb/bundles
  import ocdb/generator-utils
  import ocdb/generic-components
  import ocdb/checks

  import jitpcb/visualizer

pcb-bundle io-pin : 
  pin p

public unique pcb-component component :
  manufacturer = "Nordic Semiconductor"
  mpn ="nRF52832-QFAA-R7"
  description = "NRF52 BLE SoC"
  port dec : pin[4]
  ; port p0 : pin[32]
  port p0 : pin[{2 to 32}]
  pin vdd
  pin SWDCLK
  pin SWDIO
  pin ANT
  pin gnd
  port osc : crystal
  port lfosc : low-freq-oscillator
  pin dcc

  val ps = PinSpec $ #TABLE :
    [Ref       | Int ...   | Dir | Ref]
    [dcc    | 47 | Up | power]
    [dec[0] | 1 | Up | power]
    [dec[1] | 32 | Up | power]
    [dec[2] | 33 | Up | power]
    [dec[3] | 46 | Up | power]
    [vdd | 13 36 48 | Up | power]
    [gnd | 31  45  49 | Down | power]
    [SWDIO  | 26 | Left | io]
    [SWDCLK | 25 | Left | io]
    [osc.out  | 35 | Left | io]
    [osc.in  | 34 | Left | io]
    [ANT    | 30 | Right | io]
    ; for i in 0 to 11 do : 
    [lfosc.in |  2 | Right | io]
    [lfosc.out |  3 | Right | io]
    for i in 2 to 11 do : 
      [p0[i] | i + 2 | Right | io]
    for i in 11 to 22 do : 
      [p0[i] | i + 3 | Right | io]
    [p0[22] | 27 | Right | io]
    [p0[23] | 28 | Right | io]
    [p0[24] | 29 | Right | io]
    for i in 25 to 32 do : 
      [p0[i] | i + 12 | Right | io]

  ; for i in 0 to 32 do:
  for i in 2 to 32 do:
    supports io-pin :
      p => p0[i]
  for i in 2 to 5 do :
    supports adc :
      adc => p0[i]
  for i in 28 to 32 do :
    supports adc :
      adc => p0[i]

  make-box-symbol(ps)
  assign-package(qfn-package(0.4, 6.0, 48, 0.2, 0.4, [4.6 4.6]), ps)

  properties(osc):
    ; Max critical gain for oscillator: 4*ESR*(2*PI*F)^2*(c-shunt + c-load)^2
    ; https://infocenter.nordicsemi.com/pdf/nRF52832_PS_v1.4.pdf -- Section 19.4.2
    max-critical-gain => 0.003 ; A/V
    drive-level => 13.0e-6 ; W. 250uA^2*ESR 
    c => 1.0e-12 ; Internal capacitance
    frequency-tolerance => 40.0e-6 ; 40ppm allowed from datasheet
    frequency => 32.0e6 

  properties(lfosc):
    max-critical-gain => 3.5e-6 ; A/V
    drive-level => 6.5e-9; W. 0.25uA^2*ESR 
    c => 4.0e-12 ; Internal capacitance
    frequency-tolerance => 50.0e-6 ; 50ppm allowed from datasheet
    frequency => 32.768e3 


pcb-module t-filter :
  pin in
  pin out
  pin gnd
  inst l1 : {gen-ind-cmp(0.0027)}
  inst l2 : {gen-ind-cmp(0.0039)}
  inst c : {gen-cap-cmp(1.0e-12)}
  net (c.p[1], in)
  net (c.p[2], l1.p[1], l2.p[1])
  net (l2.p[2], out)
  net (l1.p[2], gnd)
  package(l2) at loc(0.0, 0.0) on Top
  package(l1) at loc(-1.4, 0.6, 90.0) on Top (relative-to l2)
  package(c) at loc(-2.8, 0.0) on Top (relative-to l2)

public unique pcb-module module :
  port pow3v0 : power
  pin gnd
  ; Chip antenna, with lpf, and t-filter matching network
  inst proc : {ocdb/nordic/nRF52832/component}
  net (gnd, proc.gnd, pow3v0.gnd)
  net (pow3v0.vdd, proc.vdd) 
  ; ; Decoupling caps
  cap-strap(proc.vdd, gnd, 4.7e-6)
  cap-strap(proc.vdd, gnd, 0.1e-6)
  cap-strap(proc.vdd, gnd, 0.1e-6)
  cap-strap(proc.dec[0], gnd, 0.1e-6)
  cap-strap(proc.dec[2], gnd, 0.1e-9)
  cap-strap(proc.dec[3], gnd, 1.0e-6)

  properties(proc.vdd):
    power-request => [3.0 0.1 5.0e-3]
    gnd-ref => #R(gnd)

  ; External 32 MHz crystal
  inst xtal : {ocdb/abracon/ABM12-32-B2X-T3/component}
  net (gnd, xtal.gnd)
  net XC1 (proc.osc.in, xtal.p[2])
  net XC2 (proc.osc.out, xtal.p[1])
  val load-cap = add-xtal-caps(xtal, gnd)
  check-oscillator(xtal, proc.osc, load-cap)

  ; External 32.768 kHz crystal
  inst lfxtal : {ocdb/epson/FC-135/component}
  net (proc.lfosc.in lfxtal.p[1])
  net (proc.lfosc.out lfxtal.p[2])
  val lf-cap = add-xtal-caps(lfxtal, gnd)
  check-oscillator(lfxtal, proc.lfosc, lf-cap)

  inst ant : {ocdb/johanson/2450AT18A100/component}
  inst lpf : {ocdb/johanson/2450FM07A0029/component}
  net (proc.ANT, lpf.in)
  inst t : t-filter
  net (lpf.out, t.in)
  net (t.out, ant.feed)
  net (t.gnd, lpf.gnd gnd)
  package(proc) at loc(0.0, 0.0) on Top
  package(lpf) at loc(4.7, 0.0) on Top (relative-to proc)
  package(ant) at loc(12.9, 0.0) on Top (relative-to proc)
  package(t.l2) at loc(9.6, 0.0) on Top (relative-to proc)
  layer(Courtyard(Top)) = Rectangle(6.0, 3.0, loc(6.35, 0.0))


  for i in 0 to 32 do :
    supports gpio :
      require pin:io-pin from proc
      gpio => pin.p
  for i in 0 to 8 do :
    supports adc :
      require pin:adc from proc
      adc => pin.adc
  for i in 0 to 2 do :
    supports i2c :
      require pins:io-pin[2] from proc
      sda => pins[0].p
      scl => pins[1].p
  for i in 0 to 3 do :
    supports spi :
      require pins:io-pin[4] from proc
      mosi => pins[0].p
      miso => pins[1].p
      sck => pins[2].p
      ss => pins[3].p
  for i in 0 to 2 do :
    supports uart :
      require pins:io-pin[2] from proc
      tx => pins[0].p
      rx => pins[1].p
  supports swd :
    swdio => proc.SWDIO
    swdclk => proc.SWDCLK

